buildscript {
    dependencies {
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.3.1'
    }
}

apply plugin: 'java'
apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'

sourceCompatibility = 1.7

dependencies {
    compile 'com.google.auto.service:auto-service:1.0-rc2'
    compile 'com.squareup:javapoet:1.3.0'
    compile 'com.squareup.dagger:dagger:1.2.2'
    compile project(':auto-module')

    testCompile 'junit:junit:4.12'
    testCompile 'org.assertj:assertj-core:1.7.1'
    testCompile 'com.google.testing.compile:compile-testing:0.7'
    testCompile files(org.gradle.internal.jvm.Jvm.current().getToolsJar())
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives sourcesJar
}

group = PROJECT_GROUP_ID
version = PROJECT_VERSION

if (project.rootProject.file('local.properties').exists()) {
    Properties localProperties = new Properties()
    localProperties.load(project.rootProject.file('local.properties').newDataInputStream())

    bintray {
        user = localProperties.getProperty('bintrayUser')
        key = localProperties.getProperty('bintrayApiKey')
        publications = ['AutoModulePublication']
        publish = true
        pkg {
            repo = PROJECT_REPO
            name = 'auto-module-processor'
            userOrg = user
            licenses = ['Apache-2.0']
            vcsUrl = PROJECT_VCS_URL
            version {
                name = PROJECT_VERSION
                desc = PROJECT_DESCRIPTION
            }
        }
    }

    publishing {
        publications {
            AutoModulePublication(MavenPublication) {
                from components.java
                groupId PROJECT_GROUP_ID
                artifactId 'auto-module-processor'
                version PROJECT_VERSION

                artifact sourcesJar
            }
        }
    }
}
